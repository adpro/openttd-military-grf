/* helicopter Eurocopter EC 725 */

spriteset(spriteset_air_ec725, "gfx/png/air_ec725_2cc_1x_8bpp.png") {
    // 41 x 11 x 9
    template_aircraft_voxel_zoom(0,0,8,27,28,22,37,13,1)
}
alternative_sprites(spriteset_air_ec725, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/png/air_ec725_2cc_2x_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,8,27,28,22,37,13,2)
}
alternative_sprites(spriteset_air_ec725, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "gfx/png/air_ec725_2cc_4x_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,8,27,28,22,37,13,4)
}
spriteset(spriteset_air_ec725_landing, "gfx/png/air_ec725_2cc_landing_1x_8bpp.png") {
    // 41 x 11 x 9
    template_aircraft_voxel_zoom(0,0,8,27,28,22,37,13,1)
}
alternative_sprites(spriteset_air_ec725_landing, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/png/air_ec725_2cc_landing_2x_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,8,27,28,22,37,13,2)
}
alternative_sprites(spriteset_air_ec725_landing, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "gfx/png/air_ec725_2cc_landing_4x_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,8,27,28,22,37,13,4)
}

spriteset(spriteset_air_ec725_rotor, "gfx/png/air_ec725_rotor_1x_8bpp.png") {
    template_rotor_voxel_zoom(0,0,29,15,1)
}
alternative_sprites(spriteset_air_ec725_rotor, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "gfx/png/air_ec725_rotor_2x_8bpp.png") {
    template_rotor_voxel_zoom(0,0,29,15,2)
}
alternative_sprites(spriteset_air_ec725_rotor, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "gfx/png/air_ec725_rotor_4x_8bpp.png") {
    template_rotor_voxel_zoom(0,0,29,15,4)
}

switch (FEAT_AIRCRAFT, SELF, air_ec725_status, (var[0xE2, 0, 0xFF])) {
    16..20: spriteset_air_ec725; // flight
    24: spriteset_air_ec725; // helicopter preparing to land at airport
    27: spriteset_air_ec725; // helicopter preparing to land at heliport    
        spriteset_air_ec725_landing; // default
}



if (is_aircraft_helicopter_enabled(param_aircraft_enable_eu)){
    item (FEAT_AIRCRAFT, item_air_ec725) {
        property {
            name:                           string(STR_NAME_AIR_EC725);
            climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
            introduction_date:              date(2005,02,01);
            model_life:                     50;
            /* retire_early not set, use default retirement behaviour */
            vehicle_life:                   40;
            reliability_decay:              5;
            refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_PIECE_GOODS, CC_HAZARDOUS, CC_COVERED);
            non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
            loading_speed:                  15;
            cost_factor:                    0;
            running_cost_factor:            0;
            /* cargo_age_period is left at default */

            /* airplane-specific properties */
            sprite_id:                      SPRITE_ID_NEW_AIRCRAFT; //enable new graphics
            speed:                          262 km/h;
            range:                          184;
            misc_flags:                     bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
            refit_cost:                     0; // Refitting is free
            /* callback_flags are not set, no need to manually enable callbacks */
            aircraft_type:                  AIRCRAFT_TYPE_HELICOPTER;
            acceleration:                   15;
            passenger_capacity:             29;
            mail_capacity:                  16;
            sound_effect:                   SOUND_TAKEOFF_HELICOPTER;
            /* Visual effect is left at default (no effect) */
        }
        graphics {
                additional_text:            return string(STR_DESC_AIR_EC725); //Additional text shown in purchase list
                default:                    air_ec725_status; //spriteset_air_ec725;
                rotor:                      spriteset_air_ec725_rotor;
                cost_factor:                return 28;
                running_cost_factor:        return 313;
        }
    }

    if (Aircraft_Ranges == 0) {
        item (FEAT_AIRCRAFT, item_air_ec725)
        {
            property	
            {
                range: 0; 
            }
        }
    }
    if (Aircraft_Ranges == 1) {
        item (FEAT_AIRCRAFT, item_air_ec725)
        {
            property	
            {
                range: 184; 
            }
        }
    }
}