/* airplane Avia B.35 */

spriteset(spriteset_air_avia_b35, "gfx/air_avia_b35.png") {
    // 16 x 21 x 7
    //left_x, upper_y, width, height, offset_x, offset_y
    //xoff = round.down(-xsize/2); yoff = round.down(-ysize/2)+1
    template_aircraft(0,0,9,10,13,10,12,7)
}
spriteset(spriteset_air_avia_b35_stripes, "gfx/air_avia_b35.png") {
    template_aircraft(0,11,9,10,13,10,12,7)
}
spriteset(spriteset_air_avia_b35_2cc, "gfx/air_avia_b35.png") {
    template_aircraft(0,22,9,10,13,10,12,7)
}
spriteset(spriteset_air_avia_b35_2cc_rev, "gfx/air_avia_b35.png") {
    template_aircraft(0,33,9,10,13,10,12,7)
}

switch (FEAT_AIRCRAFT, SELF, spriteset_air_avia_b35_switch, cargo_subtype)
{
  1: spriteset_air_avia_b35_stripes;
  2: spriteset_air_avia_b35_2cc;
  3: spriteset_air_avia_b35_2cc_rev;
     spriteset_air_avia_b35;
}

switch (FEAT_AIRCRAFT, SELF, air_avia_b35_cargo_subtype_text, cargo_subtype)
{
  0: string(STR_LIV_AIR_AF);
  1: string(STR_LIV_AIR_AF_STRIPES);
  2: string(STR_LIV_AIR_2CC);
  3: string(STR_LIV_AIR_2CC_REV);
     return CB_RESULT_NO_TEXT;
}

if (IS_AIRCRAFT_FIGHTER_ENABLED(param_aircraft_enable_cs)){
    item (FEAT_AIRCRAFT, item_air_avia_b35) {
        property {
            name:                           string(STR_NAME_AIR_AVIA_B35);
            climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
            introduction_date:              date(1938,09,28);
            model_life:                     4;
            /* retire_early not set, use default retirement behaviour */
            vehicle_life:                   10;
            reliability_decay:              5;
            refittable_cargo_classes:       bitmask(CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_LIQUID);
            non_refittable_cargo_classes:   bitmask(CC_PASSENGERS, CC_REFRIGERATED);
            loading_speed:                  5;
            cost_factor:                    3;
            running_cost_factor:            3;
            /* cargo_age_period is left at default */

            /* airplane-specific properties */
            sprite_id:                      SPRITE_ID_NEW_AIRCRAFT; //enable new graphics
            speed:                          550 km/h;
            range:                          188;
            misc_flags:                     bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
            refit_cost:                     0; // Refitting is free
            /* callback_flags are not set, no need to manually enable callbacks */
            aircraft_type:                  AIRCRAFT_TYPE_SMALL;
            acceleration:                   40;
            passenger_capacity:             0;
            mail_capacity:                  4;
            sound_effect:                   SOUND_TAKEOFF_PROPELLER;
            /* Visual effect is left at default (no effect) */
        }
        graphics {
                additional_text:    return string(STR_DESC_AIR_AVIA_B35); //Additional text shown in purchase list
                cargo_subtype_text: air_avia_b35_cargo_subtype_text;
                default:            spriteset_air_avia_b35_switch;
                cost_factor:                    return 1;
                running_cost_factor:            return 61;
        }
    }

    if (Aircraft_Ranges == 0) {
        item (FEAT_AIRCRAFT, item_air_avia_b35)
        {
            property	
            {
                range: 0; 
            }
        }
    }
    if (Aircraft_Ranges == 1) {
        item (FEAT_AIRCRAFT, item_air_avia_b35)
        {
            property	
            {
                range: 188; 
            }
        }
    }
}