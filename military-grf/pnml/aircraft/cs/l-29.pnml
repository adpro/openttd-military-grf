/* airplane Aero L-29 */

spriteset(spriteset_air_aero_l29, "gfx/air_aero_l29.png") {
    // 15 x 14 x 4
    //left_x, upper_y, width, height, offset_x, offset_y
    //xoff = round.down(-xsize/2); yoff = round.down(-ysize/2)+1
    template_aircraft(0,0,9,11,14,11,15,7)
}
spriteset(spriteset_air_aero_l29_stripes, "gfx/air_aero_l29.png") {
    // 15 x 14 x 4
    //left_x, upper_y, width, height, offset_x, offset_y
    //xoff = round.down(-xsize/2); yoff = round.down(-ysize/2)+1
    template_aircraft(0,12,9,11,14,11,15,7)
}
spriteset(spriteset_air_aero_l29_2cc, "gfx/air_aero_l29.png") {
    // 15 x 14 x 4
    //left_x, upper_y, width, height, offset_x, offset_y
    //xoff = round.down(-xsize/2); yoff = round.down(-ysize/2)+1
    template_aircraft(0,24,9,11,14,11,15,7)
}
spriteset(spriteset_air_aero_l29_2cc_rev, "gfx/air_aero_l29.png") {
    // 15 x 14 x 4
    //left_x, upper_y, width, height, offset_x, offset_y
    //xoff = round.down(-xsize/2); yoff = round.down(-ysize/2)+1
    template_aircraft(0,36,9,11,14,11,15,7)
}

switch (FEAT_AIRCRAFT, SELF, spriteset_air_aero_l29_switch, cargo_subtype)
{
  1: spriteset_air_aero_l29_stripes;
  2: spriteset_air_aero_l29_2cc;
  3: spriteset_air_aero_l29_2cc_rev;
     spriteset_air_aero_l29;
}

switch (FEAT_AIRCRAFT, SELF, air_aero_l29_cargo_subtype_text, cargo_subtype)
{
  0: string(STR_LIV_AIR_AF);
  1: string(STR_LIV_AIR_AF_STRIPES);
  2: string(STR_LIV_AIR_2CC);
  3: string(STR_LIV_AIR_2CC_REV);
     return CB_RESULT_NO_TEXT;
}

if (IS_AIRCRAFT_FIGHTER_ENABLED(param_aircraft_enable_cs)){
    item (FEAT_AIRCRAFT, item_air_aero_l29) {
        property {
            name:                           string(STR_NAME_AIR_AERO_L29);
            climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
            introduction_date:              date(1959,04,05);
            model_life:                     11;
            /* retire_early not set, use default retirement behaviour */
            vehicle_life:                   20;
            reliability_decay:              5;
            refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_LIQUID);
            non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
            loading_speed:                  5;
            cost_factor:                    3;
            running_cost_factor:            3;
            /* cargo_age_period is left at default */

            /* airplane-specific properties */
            sprite_id:                      SPRITE_ID_NEW_AIRCRAFT; //enable new graphics
            speed:                          655 km/h;
            range:                          180;
            misc_flags:                     bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
            refit_cost:                     0; // Refitting is free
            /* callback_flags are not set, no need to manually enable callbacks */
            aircraft_type:                  AIRCRAFT_TYPE_SMALL;
            acceleration:                   65;
            passenger_capacity:             1;
            mail_capacity:                  4;
            sound_effect:                   SOUND_TAKEOFF_JET;
            /* Visual effect is left at default (no effect) */
        }
        graphics {
                additional_text:    return string(STR_DESC_AIR_AERO_L29); //Additional text shown in purchase list
                cargo_subtype_text: air_aero_l29_cargo_subtype_text;
                default:            spriteset_air_aero_l29_switch;
                cost_factor:                    return 1;
                running_cost_factor:            return 84;
        }
    }

    if (Aircraft_Ranges == 0) {
        item (FEAT_AIRCRAFT, item_air_aero_l29)
        {
            property	
            {
                range: 0; 
            }
        }
    }
    if (Aircraft_Ranges == 1) {
        item (FEAT_AIRCRAFT, item_air_aero_l29)
        {
            property	
            {
                range: 180; 
            }
        }
    }
}