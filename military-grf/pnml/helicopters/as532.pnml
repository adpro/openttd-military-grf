/* helicopter Eurocopter SA 532 */

// 2cc
MAKE_SPRITESET_ZOOMX4(spriteset_air_as532_2cc, "air_sa532_2cc", 0,0,8,27,28,22,37,13)
MAKE_SPRITESET_ZOOMX4(spriteset_air_as532_2cc_landing, "air_sa532_2cc_landing", 0,0,8,27,28,22,37,13)
MAKE_HELI_LANDING_SWITCH(spriteset_air_as532_2cc_status, spriteset_air_as532_2cc, spriteset_air_as532_2cc_landing)

// AF
MAKE_SPRITESET_ZOOMX4(spriteset_air_as532_af, "air_sa532_af", 0,0,8,27,28,22,37,13)
MAKE_SPRITESET_ZOOMX4(spriteset_air_as532_af_landing, "air_sa532_af_landing", 0,0,8,27,28,22,37,13)
MAKE_HELI_LANDING_SWITCH(spriteset_air_as532_af_status, spriteset_air_as532_af, spriteset_air_as532_af_landing)

// liveries switch
switch (FEAT_AIRCRAFT, SELF, spriteset_air_as532_switch, cargo_subtype)
{
  1: spriteset_air_as532_2cc_status;
     spriteset_air_as532_af_status;
}

switch (FEAT_AIRCRAFT, SELF, air_as532_cargo_subtype_text, cargo_subtype)
{
  0: string(STR_LIV_AIR_AF);
  1: string(STR_LIV_AIR_2CC);
     return CB_RESULT_NO_TEXT;
}

if (IS_AIRCRAFT_HELICOPTER_ENABLED(param_aircraft_enable_eu)){
    item (FEAT_AIRCRAFT, item_air_as532) {
        property {
            name:                           string(STR_NAME_AIR_AS532);
            climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
            introduction_date:              date(1978,01,01);
            model_life:                     60;
            /* retire_early not set, use default retirement behaviour */
            vehicle_life:                   40;
            reliability_decay:              5;
            refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_PIECE_GOODS, CC_HAZARDOUS, CC_COVERED);
            non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
            loading_speed:                  15;
            cost_factor:                    0;
            running_cost_factor:            0;
            /* cargo_age_period is left at default */

            /* airplane-specific properties */
            sprite_id:                      SPRITE_ID_NEW_AIRCRAFT; //enable new graphics
            speed:                          239 km/h;
            range:                          115;
            misc_flags:                     bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
            refit_cost:                     0; // Refitting is free
            /* callback_flags are not set, no need to manually enable callbacks */
            aircraft_type:                  AIRCRAFT_TYPE_HELICOPTER;
            acceleration:                   15;
            passenger_capacity:             20;
            mail_capacity:                  17;
            sound_effect:                   SOUND_TAKEOFF_HELICOPTER;
            /* Visual effect is left at default (no effect) */
        }
        graphics {
            // name:                       foo_switch_name;
            additional_text:            return string(STR_DESC_AIR_AS532); //Additional text shown in purchase list
            cargo_subtype_text:         air_as532_cargo_subtype_text;
            default:                    spriteset_air_as532_switch;
            rotor:                      spriteset_air_sa330_rotor;
            cost_factor:                return 23;
            running_cost_factor:        return 253;
        }
    }

    SET_AIRCRAFT_RANGE_BASEDON_PARAM(item_air_as532, 0, 115)
}
