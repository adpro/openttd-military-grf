/* helicopter AgustaWestland AW101 */

// 2cc
MAKE_SPRITESET_ZOOMX4(spriteset_air_aw101_2cc, "air_aw101_2cc", 0,0,10,29,35,27,43,15)
MAKE_SPRITESET_ZOOMX4(spriteset_air_aw101_2cc_landing, "air_aw101_2cc_landing", 0,0,10,29,35,27,43,15)
MAKE_HELI_LANDING_SWITCH(spriteset_air_aw101_2cc_status, spriteset_air_aw101_2cc, spriteset_air_aw101_2cc_landing)

// AF
MAKE_SPRITESET_ZOOMX4(spriteset_air_aw101_af, "air_aw101_af", 0,0,10,29,35,27,43,15)
MAKE_SPRITESET_ZOOMX4(spriteset_air_aw101_af_landing, "air_aw101_af_landing", 0,0,10,29,35,27,43,15)
MAKE_HELI_LANDING_SWITCH(spriteset_air_aw101_af_status, spriteset_air_aw101_af, spriteset_air_aw101_af_landing)

// rotor
MAKE_SPRITESET_ROTOR_ZOOMX4(spriteset_air_aw101_rotor, "air_aw101", 0,0,29,16)

// liveries switch
switch (FEAT_AIRCRAFT, SELF, spriteset_air_aw101_switch, cargo_subtype)
{
  1: spriteset_air_aw101_2cc_status;
     spriteset_air_aw101_af_status;
}

switch (FEAT_AIRCRAFT, SELF, air_aw101_cargo_subtype_text, cargo_subtype)
{
  0: string(STR_LIV_AIR_AF);
  1: string(STR_LIV_AIR_2CC);
     return CB_RESULT_NO_TEXT;
}

if (IS_AIRCRAFT_HELICOPTER_ENABLED(param_aircraft_enable_eu)){
    item (FEAT_AIRCRAFT, item_air_aw101) {
        property {
            name:                           string(STR_NAME_AIR_AW101);
            climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
            introduction_date:              date(1997,06,01);
            model_life:                     30;
            /* retire_early not set, use default retirement behaviour */
            vehicle_life:                   40;
            reliability_decay:              5;
            refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_PIECE_GOODS, CC_HAZARDOUS, CC_COVERED);
            non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
            loading_speed:                  15;
            cost_factor:                    0;
            running_cost_factor:            0;
            /* cargo_age_period is left at default */

            /* airplane-specific properties */
            sprite_id:                      SPRITE_ID_NEW_AIRCRAFT; //enable new graphics
            speed:                          278 km/h;
            range:                          272;
            misc_flags:                     bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
            refit_cost:                     0; // Refitting is free
            /* callback_flags are not set, no need to manually enable callbacks */
            aircraft_type:                  AIRCRAFT_TYPE_HELICOPTER;
            acceleration:                   15;
            passenger_capacity:             30;
            mail_capacity:                  0;
            sound_effect:                   SOUND_TAKEOFF_HELICOPTER;
            /* Visual effect is left at default (no effect) */
        }
        graphics {
            additional_text:            return string(STR_DESC_AIR_AW101); //Additional text shown in purchase list
            cargo_subtype_text:         air_aw101_cargo_subtype_text;
            default:                    spriteset_air_aw101_switch;
            rotor:                      spriteset_air_aw101_rotor; //spriteset_air_ec725_rotor; //spriteset_air_aw101_rotor;
            cost_factor:                return 46;
            running_cost_factor:        return 418;
        }
    }

    SET_AIRCRAFT_RANGE_BASEDON_PARAM(item_air_aw101, 0, 272)
}