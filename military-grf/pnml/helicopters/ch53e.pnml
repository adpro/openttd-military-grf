/* helicopter Sikorsky CH-53E */

spriteset(spriteset_air_ch53e, "../intermediate/gfx/1x/air_ch53_2cc_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,12,40,48,37,53,18,1)
}
alternative_sprites(spriteset_air_ch53e, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "../intermediate/gfx/2x/air_ch53_2cc_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,12,40,48,37,53,18,2)
}
alternative_sprites(spriteset_air_ch53e, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "../intermediate/gfx/4x/air_ch53_2cc_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,12,40,48,37,53,18,4)
}
spriteset(spriteset_air_ch53e_landing, "../intermediate/gfx/1x/air_ch53_2cc_landing_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,12,40,48,37,53,18,1)
}
alternative_sprites(spriteset_air_ch53e_landing, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "../intermediate/gfx/2x/air_ch53_2cc_landing_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,12,40,48,37,53,18,2)
}
alternative_sprites(spriteset_air_ch53e_landing, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "../intermediate/gfx/4x/air_ch53_2cc_landing_8bpp.png") {
    template_aircraft_voxel_zoom(0,0,12,40,48,37,53,18,4)
}

spriteset(spriteset_air_ch53e_rotor, "../intermediate/gfx/1x/air_ch53_rotor_8bpp.png") {
    template_rotor_voxel_zoom(0,0,42,23,1)
}
alternative_sprites(spriteset_air_ch53e_rotor, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "../intermediate/gfx/2x/air_ch53_rotor_8bpp.png") {
    template_rotor_voxel_zoom(0,0,42,23,2)
}
alternative_sprites(spriteset_air_ch53e_rotor, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "../intermediate/gfx/4x/air_ch53_rotor_8bpp.png") {
    template_rotor_voxel_zoom(0,0,42,23,4)
}

switch (FEAT_AIRCRAFT, SELF, air_ch53e_status, (var[0xE2, 0, 0xFF])) {
    16..20: spriteset_air_ch53e; // flight
    24: spriteset_air_ch53e; // helicopter preparing to land at airport
    27: spriteset_air_ch53e; // helicopter preparing to land at heliport    
        spriteset_air_ch53e_landing; // default
}


if (is_aircraft_helicopter_enabled(param_aircraft_enable_us)){
    item (FEAT_AIRCRAFT, item_air_ch53e) {
        property {
            name:                           string(STR_NAME_AIR_CH53E);
            climates_available:             bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
            introduction_date:              date(1974,03,01);
            model_life:                     11;
            /* retire_early not set, use default retirement behaviour */
            vehicle_life:                   50;
            reliability_decay:              5;
            refittable_cargo_classes:       bitmask(CC_PASSENGERS, CC_MAIL, CC_EXPRESS, CC_ARMOURED, CC_PIECE_GOODS, CC_HAZARDOUS);
            non_refittable_cargo_classes:   bitmask(CC_REFRIGERATED);
            loading_speed:                  20;
            cost_factor:                    0;
            running_cost_factor:            0;
            /* cargo_age_period is left at default */

            /* airplane-specific properties */
            sprite_id:                      SPRITE_ID_NEW_AIRCRAFT; //enable new graphics
            speed:                          280 km/h;
            range:                          200;
            misc_flags:                     bitmask(AIRCRAFT_FLAG_2CC, AIRCRAFT_FLAG_AUTOREFIT);
            refit_cost:                     0; // Refitting is free
            /* callback_flags are not set, no need to manually enable callbacks */
            aircraft_type:                  AIRCRAFT_TYPE_HELICOPTER;
            acceleration:                   15;
            passenger_capacity:             55;
            mail_capacity:                  55;
            sound_effect:                   SOUND_TAKEOFF_HELICOPTER;
            /* Visual effect is left at default (no effect) */
        }
        graphics {
            additional_text:                return string(STR_DESC_AIR_CH53E); //Additional text shown in purchase list
            default:                        air_ch53e_status;
            rotor:                          spriteset_air_ch53e_rotor;
            cost_factor:                    return 41;
            running_cost_factor:            return 879;
        }
    }

    if (Aircraft_Ranges == 0) {
        item (FEAT_AIRCRAFT, item_air_ch53e)
        {
            property	
            {
                range: 0; 
            }
        }
    }
    if (Aircraft_Ranges == 1) {
        item (FEAT_AIRCRAFT, item_air_ch53e)
        {
            property	
            {
                range: 200; 
            }
        }
    }
}